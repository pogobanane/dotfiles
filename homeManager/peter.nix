# ~/.config/nixpgs/home.nix
# install home manager via: `nix-shell '<home-manager>' -A install`
{ config
, pkgs
, username
, homeDirectory
, my-gui
, my-noctalia ? false
, inputs
, flakepkgs
, ...
}:
let

  sendtelegram = pkgs.writeScriptBin "sendtelegram" ''
    set -e
    echo "Sending \$1 as message to me: $1"

    TOKEN=$(cat $XDG_RUNTIME_DIR/telegram_bot_token)
    CHAT_ID="272730663"
    URL="https://api.telegram.org/bot$TOKEN/sendMessage"
    MESSAGE="$1"

    [[ $(curl -s -X POST $URL -d chat_id=$CHAT_ID -d text="$MESSAGE" | ${pkgs.jq}/bin/jq .ok) = "true" ]]
  '';

  nixos-generations = pkgs.callPackage ../pkgs/nixos-generations.nix { };
  nixos-specializations = pkgs.callPackage ../pkgs/nixos-specializations.nix { };

  sopspw = pkgs.writeShellApplication {
    name = "sopspw";
    runtimeInputs = [ pkgs.sops pkgs.age ];
    text = "SOPS_AGE_KEY=$(${pkgs.age}/bin/age -d ~/.config/sops/age/keys.age) ${pkgs.sops}/bin/sops \"$@\"";
  };

  nscan = pkgs.writeShellApplication {
    name = "nscan";
    runtimeInputs = [ pkgs.nmap ];
    text = "${pkgs.nmap}/bin/nmap -sP \"$@\"";
  };

  claudeReflectRev = builtins.substring 0 12 inputs.claude-reflect-src.rev;
  claudeReflectCachePath = ".claude/plugins/cache/claude-reflect-marketplace/claude-reflect/${claudeReflectRev}";

  # gc-roots = pkgs.writeScriptBin "dont-collect-these-nix-store-paths" ''
  #   This script is a root node for the nix garbage collector. Hence the following store paths are prevented from being cleaned up.
  #   echo "${config.home-files}" # home-files get collected otherwise. Maybe its NFS?

  # '';
in
{
  imports = [
    "${inputs.sops-nix}/modules/home-manager/sops.nix"
    ./gui.nix
    ./pi
    ./calendar.nix
    ./noctalia.nix
    ./editors.nix
    # ./neovim # reset by deleting ~/.local/share/nvim/
    ./poba-nvim # reset by deleting ~/.config/poba-nvim ~/.cache/poba-nvim ~/.local/share/poba-nvim ~/.local/state/poba-nvim and re-applying home-manager activation
    inputs.nix-index-database.homeModules.nix-index
    inputs.noctalia.homeModules.default
  ];

  my-gui.enable = my-gui;
  my-noctalia.enable = my-noctalia;
  programs.nix-index-database.comma.enable = true;

  programs.nh.enable = true;

  # Configuration of secrets
  sops = {
    #age.sshKeyPaths = [ "/home/peter/.ssh/aenderpad_home_manager" ]; # must have no password!
    defaultSopsFile = ./secrets.yaml;
    age.keyFile = "${config.home.homeDirectory}/.config/sops/age/aenderpad_home_manager.txt";
    secrets.telegram_bot_token = {
      path = "%r/telegram_bot_token"; # %r gets replaced with your $XDG_RUNTIME_DIR
    };
  };

  # Let Home Manager install and manage itself.
  programs.home-manager.enable = true;
  systemd.user.startServices = "sd-switch"; # experimental way to automatically restart systemd.user services

  #programs.doom-emacs = {
  #enable = true;
  #doomPrivateDir = builtins.path {
  #name = "doom";
  #path = kjk
  #};

  # Home Manager needs a bit of information about you and the
  # paths it should manage.
  home.username = username;
  home.homeDirectory = homeDirectory;

  # This value determines the Home Manager release that your
  # configuration is compatible with. This helps avoid breakage
  # when a new Home Manager release introduces backwards
  # incompatible changes.
  #
  # You can update Home Manager without changing this value. See
  # the Home Manager release notes for a list of state version
  # changes in each release.
  home.stateVersion = "21.03";

  # this doesnt work on our arm server for some reason
  #home.file.".config/nixpkgs/config.nix".text = ''
  #  {
  #    # pin nixpkgs to same version as for NixOS
  #    pkgs ? import (${nixpkgs}){}
  #  }: {
  #      packageOverrides = pkgs: {
  #        # pin nur to same version as for NixOS
  #        nur = import (${nur}) { inherit pkgs; };
  #    };
  #  }
  #'';

  #systemd.user.services.ls1vpn = {
  #  Unit = {
  #    Description = "foobar desc";
  #  };
  #  Service = {
  #    ExecStart = "${ls1vpn}/bin/ls1vpn";
  #  };
  #};

  programs.direnv.enable = true;
  programs.direnv.nix-direnv.enable = true;

  programs.tmux = {
    enable = true;
    extraConfig = builtins.readFile ./tmux.conf;
    plugins = with pkgs; [
      tmuxPlugins.resurrect # save/restore sessions leader+S (save) and +R (restore)
      tmuxPlugins.nord # powerline-ish theme
      tmuxPlugins.pain-control # sane pane contol bindings
      tmuxPlugins.sensible # general sanity
    ];
  };

  # config.programs.ssh.startAgent = true;

  home.file.".gitconfig".source = ./gitconfig;
  home.file.".gitignore".source = ./gitignore;

  home.file.".wezterm.lua".source = ./wezterm.lua;

  home.file.".emacs.d/init.el".text = ''
    (load "default.el")
  '';

  home.file.".zshrc_actual".source = ./zshrc;
  home.file.".zshrc".text = ''
    # zsh caches compiled code in *.zwc. But its updating is broken because it
    # follows symlinks and our link target is nix store seconds since epoch 0.
    # Thus we have to force an update manually here. Note that simple
    # comparison with -nt does not work as it will always dereference the
    # symlink as well. Funnily this works of course only once the .zwc has been
    # removed manually once to bootstrap this line in. Also that file is write
    # protected (-> -f).
    [[ $(stat ~/.zshrc -c '%021Y') < $(stat ~/.zshrc.zwc -c '%021Y') ]] || rm -f ~/.zshrc.zwc

    source ${pkgs.antigen}/share/antigen/antigen.zsh
    source ~/.zshrc_actual

    # add justfile autocompletion for zsh
    export FPATH=$FPATH:${pkgs.just}/share/zsh/site-functions
  '';

  home.file.".tmate.conf".source = ./tmate.conf;
  home.file.".config/workmux/config.yaml".source = ./workmux.yaml;
  home.file.".config/lazygit/config.yml".source = ./lazygit.yml;
  home.file.".config/lazygit/git-word-diff.sh" = {
    executable = true;
    text = ''
    #!/bin/sh
    git diff --word-diff=color --no-index --color=always --no-ext-diff "$2" "$5"
    '';
  };
  home.file.".config/nono/profiles/nix-claude.json".source = ./nono-nix-claude.json;
  home.file.".claude/CLAUDE.md".source = ./user-claude.md;
  home.file.".claude/skills/academic-paper-reviewer".source = "${inputs.academic-research-skills-src}/academic-paper-reviewer";
  home.file.".claude/skills/extrasuite".source = pkgs.runCommandLocal "extrasuite-skill" { } ''
    cp -r ${inputs.extrasuite-src}/server/skills/extrasuite $out
    chmod -R +w $out
    # we dont use the uvx package manager around here
    substituteInPlace $out/SKILL.md \
      --replace-quiet 'uvx extrasuite@latest' 'extrasuite' \
      --replace-quiet 'uvx extrasuite' 'extrasuite' \
      --replace-quiet 'The `extrasuite` command is available via `uvx`. Discover commands using `--help`.' \
                      'The `extrasuite` command is already on PATH. Discover commands using `--help`.' \
      --replace-quiet '# Module overview. Skip @latest for subsequent commands' \
                      '# Module overview'
  '';

  # home.file.".claude/plugins/marketplaces/claude-reflect-marketplace".source = inputs.claude-reflect-src;
  # home.file.${claudeReflectCachePath}.source = inputs.claude-reflect-src;
  home.file.".claude/plugins/known_marketplaces.json".text = builtins.toJSON {
    claude-plugins-official = {
      source = { source = "github"; repo = "anthropics/claude-plugins-official"; };
      installLocation = "${homeDirectory}/.claude/plugins/marketplaces/claude-plugins-official";
      lastUpdated = "2025-12-17T12:49:07.220Z";
    };
    # claude-reflect-marketplace = {
    #   source = { source = "github"; repo = "bayramannakov/claude-reflect"; };
    #   installLocation = "${homeDirectory}/.claude/plugins/marketplaces/claude-reflect-marketplace";
    #   lastUpdated = "2026-04-12T00:00:00.000Z";
    # };
  };
  home.file.".claude/plugins/installed_plugins.json".text = builtins.toJSON {
    version = 2;
    # plugins."claude-reflect@claude-reflect-marketplace" = [{
    #   scope = "user";
    #   installPath = "${homeDirectory}/${claudeReflectCachePath}";
    #   version = claudeReflectRev;
    #   installedAt = "2026-04-12T00:00:00.000Z";
    #   lastUpdated = "2026-04-12T00:00:00.000Z";
    #   gitCommitSha = inputs.claude-reflect-src.rev;
    # }];
  };

  home.packages = with pkgs; [
    antigen
    zoxide
    fzf
    tree
    git
    git-absorb
    tmux
    psmisc
    # libguestfs-with-appliance
    (lazygit.overrideAttrs (prev: final: { # override  until pogoba/lazygit/word-diff is upstream
      version = "0.63.0";
      src = pkgs.fetchFromGitHub {
        owner = "pogoba";
        repo = "lazygit";
        rev = "97f1b9e2ccee12ef7b773854a1e7a11173ab27d2";
        sha256 = "sha256-SJn2sREO3q1y0A3rwNmS2tt9m7q+s3hQc4qI2G99pm8=";
      };
    }))
    delta # diff pager, e.g., for lazygit
    difftastic # diff pager, e.g., for lazygit
    ydiff # diff pager, e.g., for lazygit
    gitui
    ack
    ripgrep
    bottom # btm
    #doom-emacs
    sendtelegram
    #nix-index # nix-locate
    #comma # package containing command and run it
    sops
    sopspw
    age
    nscan
    # gc-roots
    nixos-generations
    nixos-specializations
    nix-output-monitor # nom
    # rustup
    ranger # command line file manager
    man-pages
    inputs.hosthog.packages.${stdenv.hostPlatform.system}.default
    inputs.llm-agents.packages.${stdenv.hostPlatform.system}.claude-code
    inputs.llm-agents.packages.${stdenv.hostPlatform.system}.workmux
    inputs.llm-agents.packages.${stdenv.hostPlatform.system}.ccusage
    google-cloud-sdk # needed for googleworkspace-cli
    inputs.googleworkspace-cli.packages.${stdenv.hostPlatform.system}.default # needed to log in for extrasuite
    flakepkgs.extrasuite # llm firendly google docs cli
    inputs.claude-history.packages.${stdenv.hostPlatform.system}.default
    flakepkgs.nono
    (pkgs.writeShellApplication {
      name = "nonoclaude";
      runtimeInputs = [
        # flakepkgs.nono
        # inputs.llm-agents.packages.${stdenv.hostPlatform.system}.claude-code
      ];
      text = ''
        set -e
        nono run --profile nix-claude --allow-cwd -- claude --dangerously-skip-permissions "$@"
      '';
    })

    (pkgs.writeShellApplication {
      name = "nonowrap";
      runtimeInputs = [ pkgs.bubblewrap ];
      text = builtins.readFile ../nonowrap;
    })
    bubblewrap # nice to have
    inputs.playpen.packages.${stdenv.hostPlatform.system}.playpen

    # claude-code
    codex
    gemini-cli
    github-copilot-cli
    flakepkgs.claude-monitor
    flakepkgs.nix-top
  ];
}
